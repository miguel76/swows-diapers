PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX time:   <http://www.swows.org/time#>
PREFIX tuio:  <http://www.swows.org/tuio#>

CONSTRUCT {
  swi:GraphRoot
    :pallet ?pallet .
  ?pallet
    :id ?palletId ;
    :pacco ?flyingBox .
  ?flyingBox
    :id ?id_box ;
    :startTime ?systemTime ;
    :endTime ?endTime .
}
WHERE {
  swi:GraphRoot :pallet ?pallet .
  ?pallet :id ?palletId .
  GRAPH swi:AddedGraph {
    ?pallet :quantity ?newQuantity .
  }
  GRAPH swi:DeletedGraph {
    ?pallet :quantity ?prevQuantity .
  }
  GRAPH :boxesStartingData {
    ?box
      :id ?id_box ;
      :tripTime ?tripTime .
  } .
  GRAPH :clock {
    tuio:defaultSource tuio:updateTime ?systemTime
  }
  FILTER( ?id_box <= ?prevQuantity && ?id_box > ?newQuantity ). 
  BIND( URI( CONCAT( STR(:flyingBox_), MD5(CONCAT(STR(?systemTime),STR(?palletId),STR(?id_box))) ) ) AS ?flyingBox ) .
  BIND( ?systemTime + ?tripTime AS ?endTime ) .
}

