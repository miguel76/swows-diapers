PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  swi:GraphRoot :pallet ?productType .
  ?productType
    :id ?productTypeId ;
    :quantity ?productQuantityNum .
}
WHERE {
  doc:p1 xml:hasChild ?productType .
  ?productType
    xml:hasAttribute ?productTypeIdAttr ;
    xml:hasChild ?productQuantity .
  ?productTypeIdAttr
    a xml:Attr ;
    xml:nodeName "id"^^xsd:string ;
    xml:nodeValue ?productTypeId .
  ?productQuantity
    a xml:Element ;
    xml:nodeName "available-quantity"^^xsd:string ;
    xml:hasChild ?productQuantityTxt .
  ?productQuantityTxt
    a xml:Text ;
    xml:nodeValue ?productQuantityValue .
  BIND( xsd:integer(?productQuantityValue) AS ?productQuantityNum)
}

