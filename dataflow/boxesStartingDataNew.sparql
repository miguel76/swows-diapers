PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX afn:   <http://jena.hpl.hp.com/ARQ/function#>

CONSTRUCT {
  ?box
    :id ?id_box ;
    :startPosX ?x0 ;
    :startPosY ?y0 ;
    :startSpeedX ?vx ;
    :startSpeedY ?vy ;
    :massimaAltezzaPacco ?yM ;
    :tripTime ?tripTime .
}
WHERE {
  swi:GraphRoot rdfs:member ?id_box .
  GRAPH :config {
    swi:GraphRoot
      :passoPacchiH ?passo_h ;
      :passoPacchiO ?passo_o ;
      :passoPacchiPX ?passo_p_x ;
      :passoPacchiPY ?passo_p_y  ;
      :basePacchiX ?base_x ;
      :basePacchiY ?base_y ;
      :targetPacchiX ?xF ;
      :targetPacchiY ?yF ;
      :massimaAltezzaPacchiMin ?yMin ;
      :massimaAltezzaPacchiMax ?yMax ;
      :gravitazione ?g .
  } .
  BIND( FLOOR((?id_box - 1)/ 4) AS ?level ) .
  BIND( ?id_box - FLOOR(?id_box / 4) * 4 AS ?position ) .
  BIND( ?id_box - FLOOR(?id_box / 2) * 2 AS ?right ) .
  BIND( 1 + FLOOR(FLOOR((?position - 1)/ 2) / 2) * 2 - FLOOR((?position - 1)/ 2) AS ?back ) .
  BIND( ?base_x + ?passo_o * ?right + ?passo_p_x * ?back AS ?x0 ) .
  BIND( ?base_y + ?passo_p_y * ?back + ?passo_h * ?level AS ?y0 ) .
  BIND( (?base_y - ?yMin) / (?base_y - ?yMax) AS ?k ) .
  BIND( ?y0 - (?y0 - ?yMax) * ?k AS ?yM ) .
  BIND( URI( CONCAT( STR(:box_), MD5(STR(?id_box)) ) ) AS ?box ) .
  BIND( -1 * afn:sqrt(2 * ?g * (?y0 - ?yM)) AS ?vy ) .
  BIND( -1 * ?vy / ?g + afn:sqrt((?yF - ?yM) / ?g) AS ?t ) .
  BIND( (?xF - ?x0) / ?t AS ?vx ) .
  BIND( ?t AS ?tripTime ) .
}

