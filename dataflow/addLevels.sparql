PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  swi:GraphRoot :pallet ?pallet .
  ?pallet
    :id ?palletId ;
    :quantity ?quantityLimited ;
    :level ?level .
}
WHERE {
  swi:GraphRoot :pallet ?pallet .
  ?pallet
    :id ?palletId ;
    :quantity ?productQuantityValue .
  GRAPH :config {
    swi:GraphRoot :pallet_config ?palletConfig .
    ?palletConfig
      :typeId ?palletId ;
      :maxPacchi ?maxQuantity ;
      :packsPerLevel ?packsPerLevel .
  } .
  BIND ( IF( ?productQuantityValue > ?maxQuantity, ?maxQuantity, ?productQuantityValue ) AS ?quantityLimited ) .
  BIND ( xsd:integer(CEIL(?quantityLimited / ?packsPerLevel)) AS ?level ) .
}

