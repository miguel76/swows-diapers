PREFIX :      <http://www.cattid.uniroma1.it/2012/pampers#>
PREFIX xml:   <http://www.swows.org/xml#>
PREFIX doc:   <http://www.swows.org/xml/instance#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dir:   </home/miguel/pampers3/dataflow/test_query_svg/>

CONSTRUCT {

  doc:theMenuPage	xml:listenedEventType "tuioEvent"^^xsd:string .

 doc:numero	xml:listenedEventType "tuioEvent"^^xsd:string .
 
 doc:numero xml:hasAttribute <#href_numero> .
  #doc:numero xml:listenedEventType "tuioEvent"^^xsd:string .
  <#href_numero>
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue ?href_numero_value .
	
  doc:scala xml:hasAttribute <#href_scala> .
  doc:scala xml:listenedEventType "tuioEvent"^^xsd:string .
  <#href_scala>
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue ?href_scala_value .

  doc:titolo xml:hasAttribute <#href_titolo> .
  doc:titolo xml:listenedEventType "tuioEvent"^^xsd:string .
  <#href_titolo>
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue ?href_titolo_value .
	
  doc:posizione_indicatore xml:hasAttribute <#transform_indicatore> .
  doc:posizione xml:listenedEventType "tuioEvent"^^xsd:string .
  <#transform_indicatore>
    a xml:Attr ;
    xml:nodeName "transform"^^xsd:string ;
    xml:nodeValue ?translate_indicatore .

  doc:layer_scala_colori xml:hasChild ?use_colore .
  doc:layer_scala_colori xml:listenedEventType "tuioEvent"^^xsd:string .
  ?use_colore 
    a xml:Element ;
    xml:namespace <http://www.w3.org/2000/svg> ;
    xml:nodeName "use"^^xsd:string ;
    xml:active "true"^^xsd:boolean ;
    xml:hasAttribute ?href_colore .
  ?href_colore
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue ?href_colore_value .

  doc:cerchio_grande xml:hasAttribute <#fill_cerchio_grande> .
  doc:cerchio_grande xml:listenedEventType "tuioEvent"^^xsd:string .
 	 
 <#fill_cerchio_grande>
    a xml:Attr ;
    xml:nodeName "fill"^^xsd:string ;
    xml:nodeValue ?fill_cerchio_grande_value .

  ?numero_piccolo xml:hasAttribute ?href_numero_piccolo .
  ?href_numero_piccolo
    a xml:Attr ;
    xml:nodeName "href"^^xsd:string ;
    xml:namespace <http://www.w3.org/1999/xlink> ;
    xml:nodeValue ?href_numero_piccolo_value .

  ?cerchio_piccolo xml:hasAttribute ?fill_cerchio_piccolo .
  ?fill_cerchio_piccolo
    a xml:Attr ;
    xml:nodeName "fill"^^xsd:string ;
    xml:nodeValue ?fill_cerchio_piccolo_value .

  ?menu_link_selected xml:hasAttribute ?selectedOpacity .
  ?selectedOpacity
    a xml:Attr ;
    xml:nodeName "opacity"^^xsd:string ;
    xml:nodeValue "0.3"^^xsd:string .

}
WHERE {

  GRAPH :selectedPage {
    swi:GraphRoot :selectedPage ?palletPage .
    ?palletPage
      a :Pallet ;
      :id ?selectedPalletId . 
  } .

  swi:GraphRoot
    :pallet ?pallet, ?selectedPallet .
  ?pallet
    :id ?palletId ;
    :quantity ?productQuantityValue ;
    :level ?level .
  ?selectedPallet
    :id ?selectedPalletId ;
    :quantity ?selectedProductQuantityValue ;
    :level ?selectedLevel .
  BIND( URI( CONCAT( STR(doc:numero_piccolo), "_", STR(?palletId) ) ) AS ?numero_piccolo ) .
  BIND( URI( CONCAT( STR(:href_numero_piccolo_), MD5(STR(?palletId)) ) ) AS ?href_numero_piccolo ) .
  BIND( CONCAT( "#numero_", STR(?productQuantityValue) ) AS ?href_numero_piccolo_value ) .
  BIND( URI( CONCAT( STR(doc:cerchio_piccolo), "_", STR(?palletId) ) ) AS ?cerchio_piccolo ) .
  BIND( URI( CONCAT( STR(:fill_cerchio_piccolo_), MD5(STR(?palletId)) ) ) AS ?fill_cerchio_piccolo ) .

  GRAPH :config {
    swi:GraphRoot
      :passoIndicatore ?passoIndicatore ;
      :pallet_config ?selectedPalletConfig, ?palletConfig .
    ?selectedPalletConfig
      :typeId ?selectedPalletId ;
      :color ?selectedColor_item ;
      :maxPacchi ?maxPacchi .
    ?selectedColor_item
      :id ?selectedLevel ;
      :code ?fill_cerchio_grande_value .
    ?palletConfig
      :typeId ?palletId ;
      :color ?color_item .
    ?color_item
      :id ?level ;
      :code ?fill_cerchio_piccolo_value .
  } .
  BIND( CONCAT( "#numero_", STR(?selectedProductQuantityValue) ) AS ?href_numero_value ) .
  BIND( CONCAT( "translate(0 ", STR((?maxPacchi - ?selectedProductQuantityValue) * ?passoIndicatore), ")" ) AS ?translate_indicatore ) .

  BIND( URI( CONCAT( STR(doc:menu_link), "_", STR(?selectedPalletId) ) ) AS ?menu_link_selected ) .
  BIND( URI( CONCAT( STR(:selectedOpacity_), MD5(STR(?selectedPalletId)) ) ) AS ?selectedOpacity ) .
  BIND( CONCAT( "#scala_", STR(?selectedPalletId) ) AS ?href_scala_value ) .
  BIND( CONCAT( "#titolo_", STR(?selectedPalletId) ) AS ?href_titolo_value ) .

  OPTIONAL {
    swi:GraphRoot :coloriVisualizzati ?colori .
    ?colori :colore ?num_colore
    BIND( URI( CONCAT( STR(:use_colore_), MD5(STR(?num_colore)) ) ) AS ?use_colore ) .
    BIND( URI( CONCAT( STR(:href_colore_), MD5(STR(?num_colore)) ) ) AS ?href_colore ) .
  } .
  BIND( CONCAT( "#scala_colore_", STR(?selectedPalletId), "_", STR(?num_colore) ) AS ?href_colore_value ) .

}

